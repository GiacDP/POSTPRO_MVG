PROGRAM extract_points_xz
!
! This program extract the flow signals at a list of points indicated in input file
! from the slice XZ generated by STREAmS 2.0.
! 
! Flow signals include the first 6 variables (rho, u, v, w, H, T).
! H is the total enthalphy.
! ONLY FIRST SLICE IN Y IS CONSIDERED!
!
! INPUT FILES: 
! ./points_list.dat
!   nxmax, nxmax   Streamwise and spanwise total number of points
!   nbx, nbz           "       "      "    number of blocks
!   ng             Number of ghost nodes
!   num_points     Number of sample points
!   ip, kp         Streamwise and spanwise indices of sample points
! ../slicexz_'//chx//'_000_'//chz//'.bin  Block slice binary files from simulation
!
! OUTPUT FILES: 
! ./from_slicexz_//chx_point//_//chz_point//.dat Formatted file with flow signals at point.
!
! 19/09/2023 G. Della Posta

IMPLICIT NONE

!----------------------------------------------------------------------------------

INTEGER :: nxmax, nbx, nzmax, nbz, ng, num_points
INTEGER :: nx, nz, ib_min, ib_max, kb_min, kb_max
INTEGER :: ib, kb, np, it, npl, npb, num_record, il, kl
INTEGER :: icyc, nymax, num_jslice, l, ig, kg, istatus

INTEGER, ALLOCATABLE, DIMENSION(:)   :: ip, kp, ip_block, kp_block
INTEGER, ALLOCATABLE, DIMENSION(:)   :: icyc_vec, jgloblist

INTEGER, ALLOCATABLE, DIMENSION(:,:)  :: num_points_in_block
INTEGER, ALLOCATABLE, DIMENSION(:,:)  :: ik_global, ik_local

REAL :: time

REAL, ALLOCATABLE, DIMENSION(:)       :: time_vec

REAL, ALLOCATABLE, DIMENSION(:,:,:)   :: local_signal

REAL, ALLOCATABLE, DIMENSION(:,:,:,:) :: slicexz

CHARACTER(len = 3) :: chx, chz
CHARACTER(len = 5) :: chx_point, chz_point

!----------------------------------------------------------------------------------

WRITE(*,*) '---------------------------------------------------'
WRITE(*,*) 'START extract points for xz slice.'
WRITE(*,*) '---------------------------------------------------'

!----------------------------------------------------------------------------------
! Read input
!----------------------------------------------------------------------------------

OPEN(123, file='./points_list.dat', ACTION='read', STATUS='old')
READ(123,*) 
READ(123,*) nxmax, nbx, nzmax, nbz, ng, num_points
READ(123,*) 
ALLOCATE(ip(num_points), kp(num_points))
DO np = 1, num_points
   READ(123,*) ip(np), kp(np)
ENDDO
CLOSE(123)
WRITE(*,*) 'Number of points in list: ', num_points

! For each point establish block
nx       = nxmax/nbx
nz       = nzmax/nbz
ALLOCATE(ip_block(num_points), kp_block(num_points))
ip_block = CEILING(FLOAT(ip)/nx)-1
kp_block = CEILING(FLOAT(kp)/nz)-1

! Evaluate block min e max in x and z
ib_min = MINVAL(ip_block)
ib_max = MAXVAL(ip_block)
kb_min = MINVAL(kp_block)
kb_max = MAXVAL(kp_block)

! Evaluate number of points in each block
ALLOCATE(num_points_in_block(ib_min:ib_max, kb_min:kb_max))
DO ib = ib_min, ib_max
  DO kb = kb_min, kb_max
    npb = 0
    DO np = 1, num_points
      IF ((ip_block(np) == ib) .AND. (kp_block(np) == kb)) THEN 
        npb = npb + 1
      ENDIF
    ENDDO
    num_points_in_block(ib, kb) = npb
  ENDDO
ENDDO

WRITE(*,*) 'From blocks: ib_min, ib_max = ', ib_min, ib_max
WRITE(*,*) '             kb_min, kb_max = ', kb_min, kb_max
WRITE(*,*) 'Only first slice in y is considered!'

!----------------------------------------------------------------------------------
! Read first block for sample length
!----------------------------------------------------------------------------------

WRITE(chx, '(I3.3)') ib_min
WRITE(chz, '(I3.3)') kb_min

! Read header
OPEN(135,file='../slicexz_'//chx//'_000_'//chz//'.bin',form='unformatted')
READ(135) icyc,time
READ(135) nxmax, nymax, nzmax
READ(135) num_jslice
ALLOCATE(jgloblist(num_jslice))
BACKSPACE(135)
READ(135) num_jslice,(jgloblist(l),l=1,num_jslice)
CLOSE(135)

IF (num_jslice > 1) WRITE(*,*) 'ATTENTION: num_jslice > 1!'

! Count num_record
num_record = 0
OPEN(135,file='../slicexz_'//chx//'_000_'//chz//'.bin',form='unformatted')
DO
 READ(135,iostat=istatus) icyc,time
 IF (istatus==0) then
  num_record = num_record+1
  READ(135)
  READ(135)
  READ(135)
  READ(135)
 ELSE
  EXIT
 ENDIF
ENDDO
CLOSE(135)

! Read icyc and time
ALLOCATE(time_vec(num_record))
ALLOCATE(icyc_vec(num_record))
OPEN(135,file='../slicexz_'//chx//'_000_'//chz//'.bin',form='unformatted')
DO it=1,num_record
 READ(135) icyc_vec(it),time_vec(it)
 READ(135)
 READ(135)
 READ(135)
 READ(135)
ENDDO
CLOSE(135)

WRITE(*,*) 'Number of records: ', num_record
WRITE(*,*) 'Time range = ', time_vec(1), ' - ', time_vec(num_record)

!----------------------------------------------------------------------------------
! Read each block (if there are points) and write points in block
!----------------------------------------------------------------------------------

ALLOCATE(slicexz(1-ng:nx+ng,num_jslice,1-ng:nz+ng,6))

DO ib = ib_min, ib_max
  DO kb = kb_min, kb_max

    WRITE(chx, '(I3.3)') ib
    WRITE(chz, '(I3.3)') kb

    ! List (local) indices of points in block
    npb = num_points_in_block(ib, kb) 

    WRITE(*,*) 'slicexz_', chx, '_000_', chz, ' contains :', npb, ' points.'

    IF (npb /= 0) THEN

      ALLOCATE(ik_global(npb,2), ik_local(npb,2))
      ALLOCATE(local_signal(num_record,npb,2))

      ! Local and global indices of points in block
      npl = 0
      DO np = 1, num_points
        IF ((ip_block(np) == ib) .AND. (kp_block(np) == kb)) THEN
          npl = npl + 1
          ik_global(npl,1) = ip(np)
          ik_global(npl,2) = kp(np)
          ik_local(npl, 1) = ip(np) - ib*nx 
          ik_local(npl, 2) = kp(np) - kb*nz
        ENDIF
      ENDDO

      ! Read slice and extract points
      OPEN(135,file='../slicexz_'//chx//'_000_'//chz//'.bin', &
       &       form='unformatted',status='old',action='read')
      DO it = 1, num_record
        READ(135)
        READ(135)
        READ(135)
        READ(135)
        READ(135) slicexz
        DO np = 1, npl
          il = ik_local(np,1)
          kl = ik_local(np,2)
          local_signal(it,np,1:6) = slicexz(il,1,kl,1:6) 
        ENDDO
      ENDDO
      CLOSE(135)

      ! Write history of points in block
      DO np = 1, npl
        ig = ik_global(np, 1)
        kg = ik_global(np, 2)
        WRITE(chx_point, '(I5.5)') ig
        WRITE(chz_point, '(I5.5)') kg
        OPEN(123,FILE='./from_slicexz_'//chx_point//'_'//chz_point//'.dat', &
        &        STATUS='new', ACTION='write')
        DO it = 1, num_record
          WRITE(123, '(8(ES20.10, 1X))') &
                  & icyc_vec(it), time_vec(it), local_signal(it, np, 1:6)
        ENDDO
        CLOSE(123)
      ENDDO

      ! Clean for next block
      DEALLOCATE(ik_global, ik_local, local_signal)
      CLOSE(135)

      WRITE(*,*) 'Written from slicexz_', chx, '_000_', chz
 
    ENDIF

  ENDDO 
ENDDO

DEALLOCATE(slicexz)

!----------------------------------------------------------------------------------

WRITE(*,*) '---------------------------------------------------'
WRITE(*,*) 'END extract points for xz slice.'
WRITE(*,*) '---------------------------------------------------'

END PROGRAM extract_points_xz
